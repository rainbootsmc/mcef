plugins {
    id 'fabric-loom'
    id 'maven-publish'
}

group = "net.rainbootsmc"

sourceCompatibility = JavaLanguageVersion.of(java_version as int)
targetCompatibility = JavaLanguageVersion.of(java_version as int)

java {
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
    mavenLocal()
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'net.rainbootsmc'
            artifactId = 'mcef'
            version = mcef_version
        }
    }
    def publishPath = System.getenv("PUBLISH_PATH")
    repositories {
        maven {
            url publishPath
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.06.26@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
}

sourceSets {
    jcef {
        java {
            srcDir "java-cef/java"
            exclude "**/tests/**"
        }
    }

    main {
        compileClasspath += jcef.output
        runtimeClasspath += jcef.output
    }
}

processResources.dependsOn(processJcefResources)

jar {
    from sourceSets.jcef.output.classesDirs
    from sourceSets.jcef.output.resourcesDir
}

idea {
    module {
        excludeDirs += file("java-cef/java/tests")
        inheritOutputDirs true
    }
}